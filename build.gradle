plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

defaultTasks 'jar'

group = 'com.github.org-arl'
version = "1.3.1"

targetCompatibility = 1.8
sourceCompatibility = 1.8

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'org.arl.fjage'
      artifactId = project.name
      from components.java
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/org-arl/fjage-sentuator"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
  }
}

repositories {
    maven { url 'https://oss.sonatype.org/content/groups/public' }
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
  implementation 'org.codehaus.groovy:groovy:2.5.9'
  implementation 'com.github.org-arl:fjage:1.11.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
}

compileGroovy {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

jar {
  manifest {
    attributes 'Build-Owner': System.getenv().USER, 'Build-Timestamp': new Date().format('d-MM-yyyy_HH:mm:ss'), 'Build-Version': version
  }
}

groovydoc {
  destinationDir = file("docs")
}

////// tasks for MavenCentral deployment

task javadocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from groovydoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
